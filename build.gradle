plugins {
    id "com.android.library"
    id "com.automattic.android.publish-to-s3"
    id "checkstyle"
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

checkstyle {
    toolVersion = '6.7'
}

task checkstyle(type: Checkstyle) {
    configFile = rootProject.file('checkstyle.xml')
    configProperties = ['proj.module.dir': projectDir.absolutePath]
    classpath = files()

    source 'src/main/java', 'src/debug/java'
    include '**/*.java'
    exclude '**/gen/**'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:3.0'
    testImplementation 'org.robolectric:shadows-multidex:3.0'
    testImplementation 'org.robolectric:shadows-support-v4:3.0'

    testImplementation 'org.powermock:powermock-core:1.6.5'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.5'
}

project.afterEvaluate {
    publishing {
        publications {
            PermissMePublication(MavenPublication) {
                from components.release

                groupId "com.tumblr"
                artifactId "permissme"
                artifact tasks.named("androidSourcesJar") // This task is added by 'publish-to-s3' plugin
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}
